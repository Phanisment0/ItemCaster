plugins {
	id "com.gradleup.shadow" version "9.1.0"
	id "java-library"
	id "java"
}

group = project.group
version = project.version

base {
	archivesName = project.archivesName
}

repositories {
	mavenLocal()
	mavenCentral()
	maven { url "https://hub.spigotmc.org/nexus/content/groups/public/" }
	maven { url "https://repo.papermc.io/repository/maven-public/" }
	maven { url "https://repo.codemc.io/repository/maven-public/" }
	maven { url "https://mvn.lumine.io/repository/maven-public/" }
	maven { url "https://repo.extendedclip.com/releases/" }
	maven { url "https://repo.momirealms.net/releases/" }
	maven { url "https://repo.oraxen.com/releases" }
	maven { url "https://repo.nexomc.com/releases" }
	maven { url "https://maven.devs.beer/" }
	maven { url "https://jitpack.io" }
}

dependencies {
	implementation "org.bstats:bstats-bukkit:${project.bstats_version}"
	implementation "fr.mrmicky:fastinv:${project.fastinv_version}"
	compileOnly "io.papermc.paper:paper-api:${project.api_version}"
	
	// Libraries...
	compileOnly "com.google.code.gson:gson:${project.gson_version}"
	compileOnly "me.clip:placeholderapi:${project.placeholder_api_version}"
	compileOnly "io.lumine:Mythic-Dist:${project.mythicmobs_version}"
	compileOnly "de.tr7zw:item-nbt-api-plugin:${project.nbt_api_version}"
	
	// ItemsAdder Plugin
	compileOnly "dev.lone:api-itemsadder:${project.itemsadder_version}"
	
	// Oraxen Plugin
	compileOnly "io.th0rgal:oraxen:${project.oraxen_version}"
	
	// Nexo Plugin
	compileOnly "com.nexomc:nexo:${project.nexo_version}"

	// CraftEngine Plugin
	compileOnly "net.momirealms:craft-engine-core:${project.craftengine_version}"
	compileOnly "net.momirealms:craft-engine-bukkit:${project.craftengine_version}"
}

tasks.shadowJar {
	archiveClassifier = ""
	relocate "fr.mrmicky.fastinv", "io.phanisment.itemcaster.lib.fastinv"
	relocate "org.bstats", "io.phanisment.itemcaster.lib.bstats"
	from("LICENSE") {
		into(".")
	}
	
	destinationDirectory.set(file("../server/MythicMobs/plugins"))
}

tasks.processResources {
	def props = [
		"version"		: project.version,
		"main_class": project.main_class
	]
	inputs.properties(props)
	filesMatching("plugin.yml") {
		expand(props)
	}
}

tasks.javadoc {
	title = "ItemCaster"
	destinationDir = file("docs")
	options.memberLevel = JavadocMemberLevel.PRIVATE
	options.author = true
	options.version = true
	
	options.links("https://docs.oracle.com/en/java/javase/21/docs/api/")
	options.links("https://jd.papermc.io/paper/1.21.4/")
	options.links("https://javadoc.io/doc/net.kyori/adventure-text-minimessage/latest/")
	options.linksOffline(
		"https://www.mythiccraft.io/javadocs/mythic/",
		"https://www.mythiccraft.io/javadocs/mythic/"
	)
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	toolchain.languageVersion.set(JavaLanguageVersion.of(21))
}